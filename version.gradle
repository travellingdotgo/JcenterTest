apply plugin: 'maven'
apply plugin: 'signing'


def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

def gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "." + matcher[0][1]
    } else {
        version = version + ".0"
    }

    return version
}


def gitFullVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    return version
}

def timeStamp() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

ext{
    gitVersionCode = this.&gitVersionCode
    gitVersionTag = this.&gitVersionTag
    gitFullVersionTag = this.&gitFullVersionTag
    timeStamp = this.&timeStamp
}
